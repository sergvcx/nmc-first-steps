.global __main        // объявление глобальной метки.
	
.section .data.MyData  // секция инициализированных данных
    // исходный вектор
    A: .quad  0x3333333322222222
    // место для хранения результата вычислений
    B: .quad  0x0
    // массив Matr содержит значения для заполнения матрицы весовых коэффициентов
    Matr: .quad 0x0000000100000001, \
                0xFFFFFFFF00000001
	
	
.section .text.AAA    // начало секции кода.
__main:	
    ar1 = Matr;	
	
	#if __NM4__== 0
		nb1 = 80000000h;  // разбиение матрицы на два столбца по 32 бита
		sb  = 03h;        // разбиение матрицы на две строки по 32 бита
	#else
		sir = 80000000h;  // разбиение матрицы на два столбца по 32 бита
		nb1 = sir;
		sir = 03h;        // разбиение матрицы на две строки по 32 бита
		sb  = sir;
	#endif


    
    // весовые коэффициенты загружаются в буфер wfifo, одновременно с этим они 
    // транслируются в теневую матрицу, а затем и в рабочую.
    rep 2 wfifo = [ar1++], ftw, wtw;
	
    ar2 = A;	
    ar4 = B;	
    // операция взвешенного суммирования
    rep 1 data = [ar2] with vsum , data, 0;
    // результат операции выгружается из afifo в память
    rep 1 [ar4] = afifo;
    return;	

